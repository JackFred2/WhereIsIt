plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
	id 'idea'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "whereisit-${project.minecraft_version}"
version = "1.11.3"
group = "red.jackf"

minecraft {
}

// Custom Repos
repositories {
	maven { url = "https://maven.fabricmc.net" } // Fabric
	maven { url "https://maven.shedaniel.me/" } // REI & Cloth Config
	maven { url = 'https://maven.terraformersmc.com/' } // ModMenu
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
	modCompile "me.shedaniel:RoughlyEnoughItems:${project.rei_version}"
	modCompile "com.terraformersmc:modmenu:${project.modmenu_version}"

	modCompile "me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}"
	include "me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}


// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {

			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/jackfred2/WhereIsIt")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		}
		mavenLocal()
	}
}
