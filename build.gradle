import com.modrinth.minotaur.TaskModrinthUpload

plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.archives_base_name}-${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

// Custom Repos
repositories {
	maven { url = "https://maven.fabricmc.net" } // Fabric
	maven { url = "https://maven.shedaniel.me/" } // REI & Cloth Config
	maven { url = 'https://maven.terraformersmc.com/' } // ModMenu
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
	modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

curseforge {
	if (project.hasProperty("cf.key")) {
		apiKey = project.findProperty("cf.key")
		project {
			id = "378036"
			changelog = "Check the GitHub for changes: https://github.com/JackFred2/WhereIsIt"
			releaseType = "release"

			addGameVersion "Fabric"
			addGameVersion "Java 17"

			project.game_versions.tokenize(",").each { addGameVersion it }

			mainArtifact(remapJar) {
				relations {
					requiredDependency("fabric-api")
					optionalDependency("modmenu")
					optionalDependency("roughly-enough-items")
					requiredDependency("cloth-config")
					//requiredDependency("cloth-api")
				}
				displayName = "${project.prefix} ${archivesBaseName}-${version}.jar"
			}

		}

		options {
			forgeGradleIntegration = false
		}
	}
}

modrinth {
	token = project.findProperty("modrinth.key")
	projectId = 'FCTyEqkn'
	versionNumber = project.mod_version
	versionName = "Where Is It $project.mod_version"
	versionType = "release"
	uploadFile = remapJar
	changelog = "Check the GitHub for changes: https://github.com/JackFred2/WhereIsIt"
	gameVersions = project.game_versions.tokenize(",")
	loaders = ["fabric", "quilt"]
	dependencies {
		required.project("9s6osm5g") // cloth config
		required.project("P7dR8mSH") // fabric api
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {

			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/jackfred2/WhereIsIt")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		}
		mavenLocal()
	}
}
